plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id 'org.beryx.jlink' version '2.6.4'
}

dependencies {
    implementation 'com.github.almasb:fxgl:11.0-alpha'
}

repositories {
    mavenCentral()
}

javafx {
    modules = ['javafx.controls', 'javafx.swing', 'javafx.fxml', 'javafx.media']
}

mainClassName = "org.beryx.fxgl.puzzle/org.beryx.fxgl.puzzle.SlidingPuzzle"
jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'sliding-puzzle'
        jvmArgs = ['-Dfxgl.sliding.puzzle.dummy=nothing', '-Dfxgl.sliding.puzzle.title=something']
    }
    jpackage {
        // Set the environment property BADASS_JLINK_JPACKAGE_HOME or explicitly configure the below property
        // jpackageHome = '/usr/lib/jvm/jdk13'
        def os = org.gradle.internal.os.OperatingSystem.current()
        jvmArgs = ['-Dfxgl.sliding.puzzle.dummy=nothingElse', '-Dfxgl.sliding.puzzle.title=somethingElse']
        if(os.macOsX) {
            installerOptions = [
                '--mac-sign',
/*
                '--mac-signing-key-user-name', System.getenv('SIGNING_KEY_USER_NAME'),
                '--mac-signing-keychain', System.getenv('SIGNING_KEYCHAIN_PATH')
*/
            ]
        } else if(os.windows) {
            installerOptions = ['--win-per-user-install', '--win-dir-chooser']
        }
    }
}
